// buildscript can be removed once gradle-release plugin is approved by grdle
buildscript {
    repositories {
        mavenLocal()
        if (project.hasProperty('localNexus')) {
            maven {
                url "${localNexus}"
                allowInsecureProtocol = true
            }
        } else {
            mavenCentral()
        }
    }

    dependencies {
        classpath 'com.fincher:gradle-release:1.0.0'
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "eclipse"
    id "com.diffplug.spotless" version "6.7.2"
    id 'checkstyle'
    id 'com.gradle.plugin-publish' version '1.0.0'
}

apply plugin: 'com.fincher.release'

group = 'com.fincher'

gradlePlugin {
	plugins {
		release {
			id = "com.fincher.release"
            displayName = 'Gradle release plugin'
            description = 'A plugin to perform a release in 2 phases.  Allowing for a tag and publish to be performed before the new version is set'
			implementationClass = "com.fincher.gradle.release.ReleasePlugin"
		}
	}
}
java {
	toolchain {
    	languageVersion = JavaLanguageVersion.of(11)
	}
}

release {
  requiredBranchRegex = 'master_java11'
  if (project.hasProperty('sshKeyFile')) {
    gitRepositorySshPrivateKeyFile = file("${sshKeyFile}")
  }
}

sourceSets {
	functionalTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

repositories {
    mavenLocal()
    if (project.hasProperty('localNexus')) {
        maven {
            url "${localNexus}"
            allowInsecureProtocol = true
        }
    } else {
        mavenCentral()
    }
}

publishing {
    repositories {
        maven {
            if (project.hasProperty("publishUsername") && project.hasProperty("publishPassword")) { 
                credentials {
                    username = "${publishUsername}"
                    password = "${publishPassword}"
                }
		
                if (project.version.endsWith('-SNAPSHOT')) {
                    url "${publishSnapshotUrl}"
                } else {
                    url "${publishReleaseUrl}"
                }

                allowInsecureProtocol = true

                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}
	
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

dependencies {
    implementation libs.jgit
    implementation libs.jgit.jsch
    implementation libs.guava
    testImplementation libs.junit5.api
    testImplementation libs.junit5.params
    testRuntimeOnly libs.junit5.engine
    testImplementation libs.mockito
    functionalTestImplementation libs.junit5.params
    functionalTestImplementation libs.junit5.engine
}

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
   	testClassesDirs = sourceSets.functionalTest.output.classesDirs
   	classpath = sourceSets.functionalTest.runtimeClasspath
   	useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

check {
   	// Run the functional tests as part of `check`
   	dependsOn(tasks.functionalTest)
}

test {
   	useJUnitPlatform()
}

spotless {
    java {
        eclipse('4.19.0').configFile('config/eclipse_format.xml')
    }
}
